*******************************************************************************************************(alarm)
Public Sub Export_AlarmMessage_To_Excel

Dim sDsn, sCon, sSql 
Dim conn, oRs, oCom 
Dim m, DSNName, ServerName, sServerName 
Dim sqlBeginTime,sqlEndTime
Dim sBeginTime, sEndTime 
Dim sDeep 
Dim sGMTime,sLocalTime 
Dim TimeCorrection 
Dim sFileName 
Dim sFilter 
Dim newSFilter
Dim R 
Dim Status 

Dim OldLocale 
Dim Ans 

'	OldLocale = GetLocale () 
'	If (OldLocale = 1049) Then 'Russian 
'	Else 
'	Ans = MsgBox("Пример работает только в русских региональных настройках." & vbCrLf & "Установить русские региональные настройки на время работы функции экспорта?", vbYesNo) 
'		If (Ans = vbYes) Then 
'			SetLocale(1049) 
'		Else 
'			Exit Sub 
'		End If 
'	End If 

' ms 


	If (ScreenItems("Combobox1").SelIndex=1) Then
		MsgBox ("Ошибка! Не удалось выполнить экспорт" & vbCrLf & "Для работы функции экспорта необходимо выбрать АРХИВНЫЕ данные" ) ,vbCritical,"Ошибка" 
		Exit Sub
	End If



	Set DSNName = HMIRuntime.Tags("@DatasourceNameRT") 
		DSNName.Read 
		sDsn = DSNName.Value 

	Set ServerName = HMIRuntime.Tags("@ServerName") 
		ServerName.Read 
		sServerName = ServerName.Value 


'------------------------------------------------------------------------------------------------ 
Dim objExcelApp 
	On Error Resume Next 
	Set objExcelApp = CreateObject("Excel.Application") 
	If (Err.Number > 0) Then 
		MsgBox ("Ошибка! Не удалось создать объект Excel" & vbCrLf & "Для работы функции экспорта необходимо установить MS Excel 2000\XP\2003" ) 
		'SetLocale(OldLocale) 
		Exit Sub 
	Else 
	End If 
	objExcelApp.Visible =  False
Dim ExcelSheet 
	Set ExcelSheet = CreateObject("Excel.Sheet") 
		ExcelSheet.Application.Visible = False
		ExcelSheet.ActiveSheet.Name = "Messages" 
'------------------------------------------------------------------------------------------------ 
ScreenItems("PW_Export").Visible = True 

ScreenItems("Export_2_Excel").Operation = False
HMIRuntime.Trace vbCrLf & "DSN: " & sDsn & vbCrLf 

	sDsn = "Catalog=" & sDsn & ";" 
	sCon = "Provider=WinCCOLEDBProvider.1;" & sDsn & "Data Source=" & sServerName &"\WinCC" 

	Set conn = CreateObject("ADODB.Connection") 
		conn.ConnectionString = sCon 

HMIRuntime.Trace "ADO DB conection string: " & conn.ConnectionString & vbCrLf 

		conn.CursorLocation = 3 
		conn.Open 

	Set oRs = CreateObject("ADODB.Recordset") 
	Set oCom = CreateObject("ADODB.Command") 

'------------------------------------------------------------------------------------------------ 


	

'*************Корректировка времени**********************************************	
	sGMTime = HMIRuntime.ActiveScreen.ScreenItems("SystemTime").Text 	 
	sGMTime = CDate(sGMTime)
	sLocalTime = HMIRuntime.ActiveScreen.ScreenItems("LocalTime").Text  
	sLocalTime = CDate(sLocalTime)	
	HMIRuntime.Trace "TimeZ : " & sLocalTime & " - "  & sGMTime & vbCrLf          
    TimeCorrection = DateDiff("h", sGMTime, sLocalTime)  
	
	sBeginTime=HMIRuntime.ActiveScreen.ScreenItems("FirstDate").Value
	sBeginTime=CDate(sBeginTime)
	sBeginTime= Year(sBeginTime) & "-" & Month(sBeginTime) & "-" & Day(sBeginTime) & " " & Hour("00:00:00") & ":" & Minute("00:00:00") &":" & Second("00:00:00")
	sqlBeginTime=DateAdd("h",-TimeCorrection,sBeginTime)
	sqlBeginTime= Year(sqlBeginTime) & "-" & Month(sqlBeginTime) & "-" & Day(sqlBeginTime) & " " & Hour(sqlBeginTime) & ":" & Minute(sqlBeginTime) & ":" & Second(sqlBeginTime)
	HMIRuntime.Trace "------------------------------sBeginTime : " & sBeginTime & vbCrLf 
	HMIRuntime.Trace "------------------------------sqlBeginTime : " & sqlBeginTime & vbCrLf 
	
	sEndTime=HMIRuntime.ActiveScreen.ScreenItems("SecondDate").Value
	sEndTime=CDate(sEndTime)
	sEndTime= Year(sEndTime) & "-" & Month(sEndTime) & "-" & Day(sEndTime) & " " & Hour("23:00:00") & ":" & Minute("23:59:00") &":" & Second("23:59:59")
	sqlEndTime=DateAdd("h",-TimeCorrection,CDate(sEndTime))
	sqlEndTime= Year(sqlEndTime) & "-" & Month(sqlEndTime) & "-" & Day(sqlEndTime) & " " & Hour(sqlEndTime) & ":" & Minute(sqlEndTime) & ":" & Second(sqlEndTime)
	HMIRuntime.Trace "------------------------------sEndTime : " & sEndTime & vbCrLf 
	HMIRuntime.Trace "------------------------------sqlEndTime : " & sqlEndTime & vbCrLf 
'*****************************************************************************

	sFilter = HMIRuntime.ActiveScreen.ScreenItems("FilterForExport").Text
	HMIRuntime.Trace "Данные: " & sFilter & " - " & vbCrLf
		
	sSql = "ALARMVIEW:Select * FROM AlgViewENU WHERE "  
	sSql = sSql & "DATETIME >= " & "'" & sqlBeginTime & "'"	
	sSql = sSql & "AND DATETIME <= '" & sqlEndTime & "'"
	sSql = sSql & sFilter & " Order By DateTime ASC" 'DESC ' сортировка по убыванию
    HMIRuntime.Trace "SQL String: " & sSql & vbCrLf 

	ExcelSheet.ActiveSheet.Cells(1,1).Value = "ОАО ""Алойл""" 
	ExcelSheet.ActiveSheet.Cells(1,7).Value = " " 
	ExcelSheet.ActiveSheet.Cells(2,2).Value = "ОТЧЕТ ПО АВАРИЙНЫМ И ИНФОРМАЦИОННЫМ СОБЫТИЯМ" 
	ExcelSheet.ActiveSheet.Range("A1", "H2").Font.Bold = True
	ExcelSheet.ActiveSheet.Range("A1", "H2").Font.Size = 12 
			
	ExcelSheet.ActiveSheet.Cells(3,1).Value = "Дата:" 
	ExcelSheet.ActiveSheet.Cells(3,2).Value = "от" 
	ExcelSheet.ActiveSheet.Cells(3,3).Value = CStr(DateAdd("h", TimeCorrection, sqlBeginTime))
	ExcelSheet.ActiveSheet.Cells(3,4).Value = "по" 
	ExcelSheet.ActiveSheet.Cells(3,5).Value = CStr(DateAdd("h", TimeCorrection, sqlEndTime)) 
	ExcelSheet.ActiveSheet.Rows(3).Font.Bold = True 
	ExcelSheet.ActiveSheet.Rows(3).Font.Italic = True 
	ExcelSheet.ActiveSheet.Rows(3).Font.Underline = True 
	ExcelSheet.ActiveSheet.Range("A1", "H3").Interior.Color = RGB(219,229,241) 
	
	ExcelSheet.ActiveSheet.Cells(5,1).Value = "№" 
	ExcelSheet.ActiveSheet.Cells(5,2).Value = "Статус" 
	ExcelSheet.ActiveSheet.Cells(5,3).Value = "Дата" 
	ExcelSheet.ActiveSheet.Cells(5,4).Value = "Время" 
	ExcelSheet.ActiveSheet.Cells(5,5).Value = "Класс" 
	ExcelSheet.ActiveSheet.Cells(5,6).Value = "Параметр" 
	ExcelSheet.ActiveSheet.Cells(5,7).Value = "Текст сообщения" 
	ExcelSheet.ActiveSheet.Cells(5,8).Value = "Объект" 
	
	ExcelSheet.ActiveSheet.Range("A5", "H5").Interior.ColorIndex = 15 
	ExcelSheet.ActiveSheet.Range("A5", "H5").Font.Size = 11
	ExcelSheet.ActiveSheet.Range("A5", "H5").Font.Bold = True  
	
	
	Set oCom.ActiveConnection = conn 
		oCom.CommandType = 1 
		oCom.CommandText = sSql 

	Set oRs = oCom.Execute 
		'oRs.Sort = "DateTime DESC, Ms DESC, Text2, Text1"
		'm = oRs.Fields.Count 
		m = oRs.RecordCount
HMIRuntime.Trace "БД: " & oRs.Fields.Count & " - " & oRs.RecordCount & vbCrLf 

				If oRs.RecordCount >65531 Then 
					MsgBox "Ошибка! Не возможно продолжить экспорт.Слишком много записей" & vbCrLf & "Измените диапазон выборки",vbCritical,"Ошибка" 
					Set oRs = Nothing 
					Set conn = Nothing
					ExcelSheet.Application.Quit 
					Set ExcelSheet = Nothing 
					objExcelApp.Quit 
					Set objExcelApp = Nothing
					ScreenItems("PW_Export").Visible = False
					Exit Sub
				End If




	If(m>0) Then 
		HMIRuntime.Screens("Messages.PW_Export:Export_Load").ScreenItems("Load").Value=0

		oRs.MoveFirst 
		R = 5 
		Do While Not oRs.EOF 
			HMIRuntime.Trace oRs.Fields(2).Value & "  " & oRs.Fields(37).Value  & "  " &  vbCrLf
			HMIRuntime.Screens("Messages.PW_Export:Export_Load").ScreenItems("Load").Value=((R-5)*100/oRs.RecordCount)
			R = R + 1 
			
			ExcelSheet.ActiveSheet.Cells(R,1).Value = R - 5 ' номер
			ExcelSheet.ActiveSheet.Cells(R,3).Value = CStr(FormatDateTime(DateAdd("h", TimeCorrection, oRs.Fields(2).Value), 2)) 'дата 
			ExcelSheet.ActiveSheet.Cells(R,4).Value = CStr(FormatDateTime(DateAdd("h", TimeCorrection, oRs.Fields(2).Value), 3)) 'время 
			ExcelSheet.ActiveSheet.Cells(R,5).Value = oRs.Fields(33).Value ' класс сообщения 
			ExcelSheet.ActiveSheet.Cells(R,6).Value = oRs.Fields(34).Value ' параметр 
			ExcelSheet.ActiveSheet.Cells(R,7).Value = oRs.Fields(37).Value ' текст сообщения 
			ExcelSheet.ActiveSheet.Cells(R,8).Value = oRs.Fields(38).Value ' объект 
			Status = oRs.Fields(1).Value 
			If     status = 1 Then 
				ExcelSheet.ActiveSheet.Cells(R,2).Value = "Приш" 
				ExcelSheet.ActiveSheet.Range(ExcelSheet.ActiveSheet.Cells(R, 2), ExcelSheet.ActiveSheet.Cells(R, 8)).Font.Color = oRs.Fields(49).Value 
				ExcelSheet.ActiveSheet.Range(ExcelSheet.ActiveSheet.Cells(R, 1), ExcelSheet.ActiveSheet.Cells(R, 8)).Interior.Color = oRs.Fields(50).Value 
			Elseif status = 2 Then 
				ExcelSheet.ActiveSheet.Cells(R,2).Value = "Ушло" 
				ExcelSheet.ActiveSheet.Range(ExcelSheet.ActiveSheet.Cells(R, 2), ExcelSheet.ActiveSheet.Cells(R, 8)).Font.Color = oRs.Fields(51).Value 
				ExcelSheet.ActiveSheet.Range(ExcelSheet.ActiveSheet.Cells(R, 1), ExcelSheet.ActiveSheet.Cells(R, 8)).Interior.Color = oRs.Fields(52).Value 
			Elseif status = 3 Then
				  ExcelSheet.ActiveSheet.Cells(R,2).Value = "Квит"
				  ExcelSheet.ActiveSheet.Range(ExcelSheet.ActiveSheet.Cells(R, 2), ExcelSheet.ActiveSheet.Cells(R, 8)).Font.Color = oRs.Fields(53).Value   				  
				  ExcelSheet.ActiveSheet.Range(ExcelSheet.ActiveSheet.Cells(R, 1), ExcelSheet.ActiveSheet.Cells(R, 8)).Interior.Color = oRs.Fields(54).Value	
			End If 
			ExcelSheet.ActiveSheet.Range(ExcelSheet.ActiveSheet.Cells(R, 2), ExcelSheet.ActiveSheet.Cells(R, 8)).Font.Bold = True 
			oRs.MoveNext 
		Loop 
	Else 
		ExcelSheet.ActiveSheet.Cells(6,1).Value = "Не найдено достоверных данных." & vbLf & "Проверьте временной диапазон." 
	End If 

	Set oRs = Nothing 
	Set conn = Nothing 
		ExcelSheet.ActiveSheet.Columns(1).Font.Bold = True 
		ExcelSheet.ActiveSheet.Columns(1).AutoFit 
		ExcelSheet.ActiveSheet.Columns(2).ColumnWidth=10 
		ExcelSheet.ActiveSheet.Columns(3).AutoFit 
		ExcelSheet.ActiveSheet.Columns(4).AutoFit 
		ExcelSheet.ActiveSheet.Columns(5).AutoFit 		
		ExcelSheet.ActiveSheet.Columns(6).AutoFit 		
		ExcelSheet.ActiveSheet.Columns(7).AutoFit 
		ExcelSheet.ActiveSheet.Columns(8).AutoFit 		
		
	sFileName = "D:\Отчет_о_сообщениях_от__" & Day(Date)& "." & Month(Date) & "." & Year(Date) & "__" & Hour(Now) & "-" & Minute(Now) & "-" & Second(Now) & ".xlsx" 
	ExcelSheet.SaveAs sFileName
	ScreenItems("PW_Export").Visible = False
	ScreenItems("Export_2_Excel").Operation = True

	
	'MsgBox "Отчет сохранен в файле:" & vbCrLf & sFileName ,vbInformation,"Информация"
	
	
	ExcelSheet.Application.Quit 
	Set ExcelSheet = Nothing 
	objExcelApp.Quit 
	Set objExcelApp = Nothing 
	
    OpenExcelFile(sFileName)

End Sub
*******************************************************************************************************(tag loging)
Sub OnClick(ByVal Item)                                                                                                                
			
	Dim sDsn, sCon, sSql		
	Dim conn, oRs, oCom	
	Dim m, DSNName,ServerName, sServerName	
	
	Dim objControl
	Dim sBeginTime, sEndTime
	Dim sGMTime,sLocalTime
	Dim TimeCorrection
	Dim sFileName
	Dim sTagName
	
	Dim i,j,R,C,Diff	
	Dim bVisible
	Dim bTimeCollumn	
	
	Dim OldLocale
 	Dim Ans

	OldLocale  = GetLocale ()   
	If (OldLocale = 1049) Then 'Russian
	Else  
	 Ans = MsgBox("Пример работает только в русских региональных настройках." & vbCrLf & "Установить русские региональные настройки на время работы функции экспорта?", vbYesNo)	 
	 If (Ans = vbYes) Then  
	  SetLocale(1049)	 
	 Else   
	   Exit Sub
	 End If    	 
	End If	
  
	Set DSNName = HMIRuntime.Tags("@DatasourceNameRT")
	DSNName.Read
	sDsn = DSNName.Value	
	
	Set ServerName = HMIRuntime.Tags("@ServerName")
	ServerName.Read
	sServerName = ServerName.Value	
'------------------------------------------------------------------------------------------------   		
Dim objExcelApp, ExcelSheet
On Error Resume Next 
Set objExcelApp = CreateObject("Excel.Application")
If (Err.Number > 0) Then 
 MsgBox ("Ошибка!  Не удалось создать объект Excel" & vbCrLf & "Для работы функции экспорта необходимо установить MS Excel 2000\XP\2003" )   
 SetLocale(OldLocale)
 Exit Sub
Else 
End If 
objExcelApp.Visible = True
Set ExcelSheet = CreateObject("Excel.Sheet")
ExcelSheet.Application.Visible = True
ExcelSheet.ActiveSheet.Name = "ArchValues"
'------------------------------------------------------------------------------------------------   	
 ScreenItems("PW_Export_1").Visible = True  
 ScreenItems("PW_Export_2").Visible = True  
 
HMIRuntime.Trace vbCrLf & "DSN: " & sDsn & vbCrLf
	
	sDsn = "Catalog=" & sDsn & ";"
	sCon = "Provider=WinCCOLEDBProvider.1;" & sDsn & "Data Source=" & sServerName &"\WinCC"	
    
    Set conn = CreateObject("ADODB.Connection")
    conn.ConnectionString = sCon
    
HMIRuntime.Trace "ADO DB conection string: " & conn.ConnectionString & vbCrLf
    
    conn.CursorLocation = 3
    conn.Open    
    
    Set oRs = CreateObject("ADODB.Recordset")
    Set oCom = CreateObject("ADODB.Command")    

'------------------------------------------------------------------------------------------------   	
HMIRuntime.Trace "Control1: start"  & vbCrLf          
	'TimeCorrection = 4 ' можно брать из региональных настроек	
	sGMTime = ScreenItems("Date1").Text
	sLocalTime  = ScreenItems("Date2").Text
	sGMTime = CDate(sGMTime)
	sLocalTime = CDate(sLocalTime)	
HMIRuntime.Trace "TimeZ : " & sLocalTime & " - "  & sGMTime & vbCrLf          
    TimeCorrection = DateDiff("h", sGMTime, sLocalTime)    
HMIRuntime.Trace "TimeZone : " & TimeCorrection & vbCrLf          
    
	Set objControl = ScreenItems("TrendsControl")
	C = 2
	bTimeCollumn = False
For i = 0 To objControl.NumItems - 1
	objControl.Index = i
	bVisible = objControl.ItemVisible		
HMIRuntime.Trace "№: " & i & " ; "  & bVisible & vbCrLf      
	If bVisible Then 
	    C = C + 1
		sTagName = objControl.TagName			 
		ExcelSheet.ActiveSheet.Cells(2,C).Value = objControl.Name				
		ExcelSheet.ActiveSheet.Cells(2,C).Font.Color = objControl.Color		
		ExcelSheet.ActiveSheet.Cells(2,C).Font.Size = 10
		ExcelSheet.ActiveSheet.Cells(2,C).Font.Bold = True
		ExcelSheet.ActiveSheet.Cells(2,C).Interior.ColorIndex = 15
				
		If Not bTimeCollumn Then
     		ExcelSheet.ActiveSheet.Cells(2,1).Value = "№"	
			ExcelSheet.ActiveSheet.Cells(2,1).Font.Size = 10
			ExcelSheet.ActiveSheet.Cells(2,1).Font.Bold = True	
			ExcelSheet.ActiveSheet.Cells(2,1).Interior.ColorIndex = 15
     		ExcelSheet.ActiveSheet.Cells(2,2).Value = "Дата/Время"
			ExcelSheet.ActiveSheet.Cells(2,2).Font.Size = 10
			ExcelSheet.ActiveSheet.Cells(2,2).Font.Bold = True
			ExcelSheet.ActiveSheet.Cells(2,2).Interior.ColorIndex = 15
			sBeginTime = objControl.BeginTime
			sEndTime   = objControl.EndTime	
			
			ExcelSheet.ActiveSheet.Cells(1,3).Value = CStr(sBeginTime) ' & vbLf & CStr(sEndTime)			
			ExcelSheet.ActiveSheet.Cells(1,4).Value = CStr(sEndTime)
			
			sBeginTime = DateAdd("h", -TimeCorrection, sBeginTime)	
			sEndTime   = DateAdd("h", -TimeCorrection, sEndTime)
			
			Diff = DateDiff("n", sBeginTime, sEndTime)
			HMIRuntime.Trace "Time: " & Diff & vbCrLf					
		Else 
		End If		
HMIRuntime.Trace "T2: " & sBeginTime & " --- " & sEndTime & vbCrLf				
		sSql = "Tag:R, '" & sTagName & "', '" & Year(sBeginTime) &	"-" & Month(sBeginTime)  & "-" & Day(sBeginTime) & " " & Hour(sBeginTime) & ":" & Minute(sBeginTime) & ":" & Second(sBeginTime) & ".000', '" & Year(sEndTime) & "-" & Month(sEndTime)  & "-" & Day(sEndTime) & " " & Hour(sEndTime) & ":" & Minute(sEndTime) & ":" & Second(sEndTime) & ".000' "    
HMIRuntime.Trace "SQL string: " & sSql & vbCrLf		
	
		oCom.CommandType = 1 
    	Set oCom.ActiveConnection = conn
	    oCom.CommandText = sSql
	    
	    Set oRs = oCom.Execute
	    m = oRs.Fields.Count  
HMIRuntime.Trace "БД: " & oRs.Fields.Count & " - " & oRs.RecordCount & vbCrLf      	    
	    If(m>0) Then
		    oRs.MoveFirst
	   		R = 2
		    Do While Not oRs.EOF
    	   		R = R + 1
				ExcelSheet.ActiveSheet.Cells(R,C).Value = oRs.Fields(2).Value
				If Not bTimeCollumn Then
  				 ExcelSheet.ActiveSheet.Cells(R,2).Value = CStr(DateAdd("h", TimeCorrection, oRs.Fields(1).Value)) 'дата время 
'  				 ExcelSheet.ActiveSheet.Cells(R,2).Value = CStr(FormatDateTime(DateAdd("h", TimeCorrection, oRs.Fields(1).Value), 2)) 'дата
'  				 ExcelSheet.ActiveSheet.Cells(R,3).Value = CStr(FormatDateTime(DateAdd("h", TimeCorrection, oRs.Fields(1).Value), 3)) 'время
  				 ExcelSheet.ActiveSheet.Cells(R,1).Value = R - 2 
				Else 
				End If
				oRs.MoveNext
		    Loop		    
	    Else
    		ExcelSheet.ActiveSheet.Cells(R,C).Value = "Не найдено достоверных данных."  & vbLf & "Проверьте временной диапазон."
	    End If
	    bTimeCollumn = True				
    Else
    End If
Next  	    
HMIRuntime.Trace "Control1: stop"  & vbCrLf             

  ExcelSheet.ActiveSheet.Cells(1,1).Value = R - 2
  ExcelSheet.ActiveSheet.Cells(1,2).Value = C - 2


    Set oRs = Nothing
    Set conn = Nothing														        
    ExcelSheet.ActiveSheet.Columns(1).Font.Bold = True    
    ExcelSheet.ActiveSheet.Rows(1).Font.Bold = True
    ExcelSheet.ActiveSheet.Rows(1).Font.Italic = True
    ExcelSheet.ActiveSheet.Rows(1).Font.Underline = True        
    ExcelSheet.ActiveSheet.Columns.AutoFit     
    
    
	sFileName = "C:\Отчет_от__" & Day(Date)& "." & Month(Date) & "." & Year(Date) & "__" & Hour(Now) & "-" & Minute(Now) & "-" & Second(Now) & ".XLS"
'    sFileName = "C:\Архивные_значения.XLS"
	ExcelSheet.SaveAs sFileName
	MsgBox "Отчет сохранен в файле:" & vbCrLf & sFileName
	ScreenItems("Report").Text = sFileName
	ExcelSheet.Application.Quit	
	Set ExcelSheet = Nothing
	objExcelApp.Quit
	Set objExcelApp = Nothing  	
	
	ScreenItems("PW_Export_1").Visible = False
	ScreenItems("PW_Export_2").Visible = False	
	SetLocale(OldLocale)
End Sub
*******************************************************************************************************
